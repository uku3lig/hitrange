plugins {
    id 'fabric-loom' version '1.5-SNAPSHOT'
    id 'org.ajoberstar.grgit' version '5.0.0'
    id 'io.freefair.lombok' version '8.4'
}

version = "${project.mod_version}+mc${project.minecraft_version}${grgit.status().clean ? '' : '-dirty'}"
group = project.maven_group

loom {
    accessWidenerPath = file("src/main/resources/hitrange.accesswidener")
}

repositories {
    maven {
        url "https://maven.uku3lig.net/releases"
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modApi "net.uku3lig:ukulib:${project.ukulib_version}"
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
}

java {
    archivesBaseName = project.archives_base_name

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}
